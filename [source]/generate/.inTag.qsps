# dialog.inTag
$args[0] = $args[0]	&	!	тело диалога
! args["set1"]=msecscount	&	!	запоминаем время на начало конвертации
! длина открывающего тега 14 закрывающего 15. Меняем открывающие и закрывающие теги на более развёрнутые
$args[0]=$replace($args[0],'[:','[answer000000:')
$args[0]=$replace($args[0],':]',':answer0000000]')
$args[0]=$replace($args[0],'{:','{quest0000000:')
$args[0]=$replace($args[0],':}',':quest00000000}')
args['i']=1	&	!	счётчик
:find_next_tag
if arrsize('easy_dialog_temp_var1')=0: easy_dialog_temp_var1[]-=1
$args['mid']=$mid($args[0],easy_dialog_temp_var1[]+1)
args['tag.instr']=strpos($args['mid'],'\[answer000000:|:answer0000000\]|\{quest0000000:|:quest00000000\}')+(len($args[0])-len($args['mid']))
!*pl "Получен тег в позиции <<args['tag.instr']>>"
!wait 500
if args['tag.instr']<>0:
	args['tag.instr.memory']=args['tag.instr']
	if $mid($args[0],args['tag.instr.memory'],1)='[' or $mid($args[0],args['tag.instr.memory'],1)='{':
		$args['tag.type.temp']=$mid($args[0],args['tag.instr.memory'],14)
	else
		$args['tag.type.temp']=$mid($args[0],args['tag.instr.memory'],15)
	end
	!*pl "Тип тега <<$args['tag.type.temp']>>"
	!wait 500
	if $args['tag.type.temp']='[answer000000:' or $args['tag.type.temp']='{quest0000000:':
	!*pl "Открывающий тег, запоминаем"
	!wait 500
	! Если тег открывающий, заносим вхождение в мини-массив
		easy_dialog_temp_var1[]=args['tag.instr.memory']
		$easy_dialog_temp_var2[]=$args['tag.type.temp']
		jump 'find_next_tag'
	elseif $args['tag.type.temp']=':answer0000000]' or $args['tag.type.temp']=':quest00000000}':
	! если тег закрывающий
	!*pl "Закрывающий тег"
	!wait 500
		if	($args['tag.type.temp']=':answer0000000]' and $easy_dialog_temp_var2[]='[answer000000:') or  _
			($args['tag.type.temp']=':quest00000000}' and $easy_dialog_temp_var2[]='{quest0000000:'):
			!*pl "Закрывающий тег совпадает с открывающим. Подменяем теги"
			!wait 500
			$args['string.prev.open']=$mid($args[0],1,easy_dialog_temp_var1[]-1)
			$args['string.post.open']=$mid($args[0],easy_dialog_temp_var1[]+14,args['tag.instr.memory']-(easy_dialog_temp_var1[]+14))
			$args['string.post.close']=$mid($args[0],args['tag.instr.memory']+15)
			if $args['tag.type.temp']=':answer0000000]': $args['newtag']='answer'+$func('#zero#',6-len(str(args['i'])))
			if $args['tag.type.temp']=':quest00000000}': $args['newtag']='quest'+$func('#zero#',7-len(str(args['i'])))
			$args[0] = $args['string.prev.open']+"<"+$args['newtag']+str(args['i'])+">"+$args['string.post.open']+'</'+$args['newtag']+str(args['i'])+'>'+$args['string.post.close']
			args['type.r']=arrsize('$easy_dialog_temp_var2')-1
			args['pos.r']=arrsize('$easy_dialog_temp_var1')-1
			killvar '$easy_dialog_temp_var2',args['type.r']
			killvar 'easy_dialog_temp_var1',args['pos.r']
			args['i']+=1
			jump 'find_next_tag'
		else
		! если найден закрывающий тег, не совпадающий с предыдущим открывающим, это ошибка
			msg 'Закрывающий тег не соответствует открывающему, см. лог ошибок.'
			$log_error[]="Несоответствие типов закрывающего и открывающего тегов. Реплика: "+$mid($args[0],easy_dialog_temp_var1[]+14,args['tag.instr.memory']-(easy_dialog_temp_var1[]+14))
		end
	end
end
! args["set2"]=msecscount
$result=$args[0]+'
<!--[count-tag:'+str(args['i'])+']-->'
killvar '$easy_dialog_temp_var2'
killvar 'easy_dialog_temp_var1'

--- dialog.inTag ---------------------------------
