# dialog.generate
$args[0] = $args[0]	&	!	тело диалога (исходник)
$args[1] = $args[1]	&	!	уникальный идентификатор корневой реплики
$args[2] = $args[2]	&	!	управление: get.objs — генерируем QSP-код для вставки в игру, get.base — генерируем QSP-код в виде готовой базы, пустое значение — заполняем наборы массивов сгенерированными данными
if arrsize('$id_replic')=0:
! если таблица объектов диалогов не была реализована
	! создаём объект "пустая реплика"
	$id_replic[]='DIALOGS'
	$source_replic[]='<dialogs>'
	$sets_replic[]=''
	kolvo_replic[]=0
	$position_replic[]=''
end
args['sepuku1']=msecscount	&	!	запоминаем начальное время генерации
! конвертируем диалог в тегированный вид:
$args[0]=$func('dialog.inTag',$args[0])
! помещаем диалог в микробазу:
gosub 'dialog.tag.inArr',$args[0],$args[1]
! сообщаем настройки дочерним объектам
gosub 'dialog.sets.transport',$args[1],'generate'
! заменяем айдишники реплик
gosub 'dialog.replace.ID',$args[1]
! --------------------------------------------------------- переносим диалог в базу диалогов ----------------------------------------------------
! Если нужно сформировать диалог для базы, вовзращаем текущее число меток в базе
if $args[2]='get.base': args['b']=DIALOG_VALUE['base.count']	&	!	base.count - счётчик меток в базе
args['i']=0	&	!	счётчик цикла
:for
if args['i']<arrsize('$easy_dialog_base_ID'):
	if $args[2]="get.base" and (args['i'] mod 10 = 0):
		$args['result']+=":DIALOG_<<args['b']>>
"
		args['b']+=1
	end
	if $args[2]="get.base" or $args[2]="get.objs":
		$args['result']+="! REPLIC_<<args['i']>>
"
		$args['result']+="$id_replic[]='"+$func('#widetrim#',$easy_dialog_base_ID[args['i']],'[trim]')+"'
"
		$args['result']+="$source_replic[]='"+$replace($func('#widetrim#',$easy_dialog_base_SC[args['i']],'[trim]'),"'","''")+"'
"
		$args['result']+="$sets_replic[]='"+$replace($func('#widetrim#',$easy_dialog_base_ST[args['i']],'[trim]'),"'","''")+"'
"
		$args['result']+="kolvo_replic[]="+$str(easy_dialog_base_KOL[args['i']])+"
"
		$args['result']+="$position_replic[]='"+$func('#widetrim#',$easy_dialog_base_PS[args['i']],'[trim]')+"'
"
	else
		$id_replic[]=$easy_dialog_base_ID[args['i']]
		$source_replic[]=$easy_dialog_base_SC[args['i']]
		$sets_replic[]=$easy_dialog_base_ST[args['i']]
		kolvo_replic[]=easy_dialog_base_KOL[args['i']]
		$position_replic[]=$easy_dialog_base_PS[args['i']]
	end
	args['i']+=1
	if $args[2]="get.base" and (args['i'] mod 10 = 0):
		$args['result']+="jump 'end_sel'
"
	end
	jump 'for'
elseif $args[2]="get.base":
	$args['result']+="jump 'end_sel'"
end
DIALOG_VALUE['base.count']=args['b']+1
gosub 'dialog.killtemp'
args['sepuku2']=msecscount
if $args['result']<>'':
	$result=$args['result']+"
! диалог сгенерирован за <<args['sepuku2']-args['sepuku1']>> мсек."
else
	$source_replic[arrsize('$source_replic')-1]=$source_replic[arrsize('$source_replic')-1]+"<!-- диалог сгенерирован за <<args['sepuku2']-args['sepuku1']>> мсек.-->"
end
--- dialog.generate ---------------------------------
