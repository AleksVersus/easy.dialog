QSP-Game Интерпретатор отдельной реплики
$args[0] -	идентификатор реплики
# dialog.replic.int
!@ ------------------- многократно используемы локальные переменные ------------------- 
	local $code_
!@ ------------------- многократно используемы локальные переменные ------------------- 
local $rcode_ = $dialogs_run[$args[0]]	&	!@	Динамический код, повешенный на реплику

if $rcode_ <> '': dynamic $rcode_	&	!@	выполняем код текущей реплики
@dialog.replic.print($args[0])		&	!@	выводим текущую реплику на экран

!@ снимаем фиксацию с реплики
$DIALOG_VALUE['list.act.fix'] = @edb.list.remove($DIALOG_VALUE['list.act.fix'], $args[0])

!@ Рекурсия к вложенным репликам. Получаем список реплик
local i=0	&	!@	общий счётчик проходов
local a=0	&	!@	счётчик ответных реплик
local q=0	&	!@	счётчик вопросных реплик

!@ добавляем фиксированные ответные реплики в список
local $answer_pos_ & !@ массив для списка ответных реплик
$code_ = { $answer_pos_[a] = $args[0] & a += 1 }
@edb.list.for_each($DIALOG_VALUE['list.act.fix'], $code_)

!@ ---------------------------- перебираем дочерние реплики и составляем два списка ---------------------------- 
	local $rdaughters_ = $dialogs_includes[$args[0]]&	!@	список дочерних
	local maximal_, minimal_
	$code_ = {
		if @dialog.replic.prv.if($args[0]):
			!@ если реплика прошла проверку на соответствие условиям
			!@ определяем тип реплики
			local $type_ = @em.tag.getNum($dialogs_sets[$args[0]], 'type')
			if $type_ = 'quest':
				!@ если это реплика неписи (пассивная)
				local pos_ = @dialog.mb.add($args[0])
				if maximal_['count'] < REPLICS_COUNT[pos_]:
					!@ если максимальное из просмотреннных значений меньше текущего, выбираем его
					maximal_['count'] = REPLICS_COUNT[pos_] &	!@	запоминаем новое максимальное значение
					maximal_['pos'] = pos_					&	!@	запоминаем положение значения в микробазе
				end
				if minimal_['count'] >= REPLICS_COUNT[pos_]:
				! если минимальное из просмотреннных значений больше текущего или равно, выбираем его
					minimal_['count'] = REPLICS_COUNT[pos_]	&	!@	запоминаем новое минимальное значение
					minimal_['pos'] = pos_					&	!@	запоминаем положение значения в микробазе
				end
				!@ если минимальное количество равно нулю, а минимальное значение в микробазе — нет,
				!@ минимальное количество выставляем по минимальному в микробазе
				if minimal_['count'] = 0 and min('REPLICS_COUNT')<>0: minimal_['count'] = min('REPLICS_COUNT')
				q+=1
			end
			!@ если это реплика героя (ответная), запоминаем её позицию в базе
			if $type_ = 'answer': $answer_pos_[a] = $args[0] & a += 1
		end
	}
	@edb.list.for_each($rdaughters_, $code_)
!@ ---------------------------- перебираем дочерние реплики и составляем два списка ---------------------------- 


local $rsets_ = $dialogs_sets[$args[0]]		&	!@	настройки реплики
local $rtype_ = @em.tag.getNum($rsets_, 'type') &	!@	Тип реплики

local $now_sets_ = @dialog.get.sets($args[0])	& !@ текущие настройки, восстановленные до корня ветки
local $replics_repeat_ = @em.tag.getNum($now_sets_, 'repeat')	& !@ число повторов реплики
local $replics_shuffle_ = @em.tag.getNum($now_sets_, 'shuffle')	& !@ порядок воспроизведения

!@ когда есть список активируемых реплик. Выгружаются все.
if a > 0:
	!@ Если текущая реплика пассивная, очищаем список кнопок на экране; в противном случае — дополняем:
	local $answer_buttons_ = $iif($rtype_ = 'quest', '', @dialog.screen.get_btns())
	loop local i = 0 while i<a step i += 1:
		if $dialogs_id[$answer_pos_[i]]<>'' and no @edb.list.is_el($DIALOG_VALUE['list.act.del'], $answer_pos_[i]):
			$answer_buttons_ += @dialog.int.answer($answer_pos_[i], $now_sets_)
		end
	end
end


!@ список реплик неписи. Из набора реплик выбирается одна
if q > 0:
	if $replics_repeat_='one':
		!@ Если фраза выводится всё время одна и та же.
		if maximal_<>minimal_:
			!@ Если минимальное и максимальное значения не совпадают, значит одна из фраз уже выбрана
			args['mem.pos']=easy_dialog_temp_var1[args['maximal.pos']]	&	!	запоминаем позицию реплики (в таблице данных) с максимальным значением в kolvo_replic
			! удаляем бесполезные массивы:
			killvar 'easy_dialog_temp_var1'
			killvar 'easy_dialog_temp_var2'
			! восстанавливаем позицию и количество в массивы:
			easy_dialog_temp_var1[]=args['mem.pos']
			easy_dialog_temp_var2[]=kolvo_replic[args['mem.pos']]
		end
	end
	if $replics_repeat_='once':
	! Если реплики выводятся один круг и после выводится только последняя
		if maximal_=minimal_:
		! если минимум и максимум совпадают, выбираем реплику
			! снимаем метки со всех реплик
			:del_kol
			i=0
			:for_once_ren
			if i<arrsize('easy_dialog_temp_var1'):
				kolvo_replic[easy_dialog_temp_var1[i]]=0
				i+=1
				jump 'for_once_ren'
			end
			jump 'choose_frase'
		elseif maximal_<>minimal_ and minimal_=0:
		! Если метки стоят на паре фраз, а на других не стоят, выбираем только неотмеченные реплики
			! удаляем неотмеченные реплики
			:del_once_del
			i=0
			:for_once_del
			if i<arrsize('easy_dialog_temp_var1'):
				if easy_dialog_temp_var2[i]<>0:
					killvar 'easy_dialog_temp_var2',i
					killvar 'easy_dialog_temp_var1',i
				else
					i+=1
				end
				jump 'for_once_del'
			end
			if arrsize('easy_dialog_temp_var1')=1 and $replics_repeat_='once': kolvo_replic[easy_dialog_temp_var1[0]]+=1
			jump 'choose_frase'
		elseif maximal_<>minimal_ and minimal_>0:
		! Если реплики уже выбирались и на одной из них есть метка
		! выбираем эту реплику
			killvar 'easy_dialog_temp_var1'
			killvar 'easy_dialog_temp_var2'
			easy_dialog_temp_var1[]=args['maximal.pos']
		end
	end
	if $replics_repeat_='cicle':
		if maximal_=minimal_:
			jump 'del_kol'
		else
			jump 'del_once_del'
		end
	end
	:choose_frase
	if $replics_shuffle_='random':
		$args['chosing frase']=$id_replic[easy_dialog_temp_var1[rand(0,arrsize('easy_dialog_temp_var1')-1)]]
	end
	if $replics_shuffle_='straight':
		$args['chosing frase']=$id_replic[easy_dialog_temp_var1[0]]
	end
	! удаляем бесполезные массивы:
	killvar 'easy_dialog_temp_var1'
	killvar 'easy_dialog_temp_var2'
	if $args['chosing frase']<>'': gosub 'dialog.int.quest',$args['chosing frase']
end
if instr($args['source'],'closeup')<>0: gosub 'dialog.closeup' & exit		&	!	закрытие диалога происходит после выполнения кода и вывода реплики на экран
--- dialog.replic.int ---------------------------------

# dialog.mb.add
local pos_ = arrpos('$REPLICS_ID', $args[0])
if pos_ = -1:
	pos_ = arrsize('$REPLICS_ID')
	REPLICS_COUNT[] = 0
	$REPLICS_ID[] = $args[0]
end
result = pos_
--- dialog.mb.add ---------------------------------

