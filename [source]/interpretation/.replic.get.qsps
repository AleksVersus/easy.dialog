# dialog.replic.get
$args[0] = $args[0]			&	!	идентификатор реплики
!$args[1] = UCASE($args[1])	&	!	управляющая конструкция
$args['strip']='[strip]'	&	!	if instr($args[1],'[NO TRIM]')=0: else $args['trim']=''
$args['source']=$source_replic[arrpos('$id_replic',$args[0])]
! списко настроек
:for_i
if args['i']=0:	$args['regular']='<!--([\s\S]*?)-->'					&	!	удаление комментариев
if args['i']=1:	$args['regular']='npc=("|'')([\s\S]*?)\1'				&	!	удаление настроек неписи
if args['i']=2:	$args['regular']='hero=("|'')([\s\S]*?)\1'				&	!	удаление настроек героя
if args['i']=3:	$args['regular']='replics=("|'')([\s\S]*?)\1'			&	!	удаление настроек реплик
if args['i']=4:	$args['regular']='frase-block:([\s\S]*?):frase-block'	&	!	удаление блока
if args['i']=5:	$args['regular']='levelup:(\d*)'						&	!	удаление левелапов
if args['i']=6:	$args['regular']='leveljump:(\S*)'						&	!	удаление левелджампов
if args['i']=7:	$args['regular']='marker:(\S*)'							&	!	удаление меток
if args['i']=8:	$args['regular']='dynamic-code:([\s\S]*?):dynamic-code'	&	!	удаление динамического кода
if args['i']=9:	$args['regular']='iffing:([\s\S]*?):iffing'				&	!	удаление ифингов
if args['i']=10: $args['regular']='closeup' 							&	!	удаление клоузапов
if args['i']=11: $args['regular']='act-name:([\s\S]*?):act-name' 		&	!	удаление коротких названий действий
if args['i']=12: $args['regular']='selectact\.delete'					&	!	удаление селактов

if args['i']<13:
	:for_start
	$args['comment']=$strfind($args['source'],$args['regular'])
	if $args['comment']<>'':
		$args['source']=$replace($args['source'],$args['comment'])
		jump 'for_start'
	end
	args['i']+=1
	jump 'for_i'	&	!	убрано if instr($args[1],'[DELCOMM]')=0: 
end
$args['source']=$func('#widetrim#',$args['source'],$args['strip'])
$result=$args['source']
--- dialog.replic.get ---------------------------------
