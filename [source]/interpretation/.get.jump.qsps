# dialog.get.jump
$args[0] = $args[0]													&	!	идентификатор реплики
args['pit']=arrpos('$id_replic',$args[0])							&	!	позиция реплики в минибазе
$args['source']=$source_replic[args['pit']]							&	!	исходник реплики
! поиск реплики для перехода вверх по уровням
args['levelup']=func('get.tag.num',$args['source'],'levelup')
$args['lup']=$args[0]
:levelup
args['pit.lup']=arrpos('$id_replic',$position_replic[arrpos('$id_replic',$args['lup'])])
if args['levelup']>0 and $position_replic[args['pit.lup']]<>'':
	$args['lup']=$id_replic[args['pit.lup']]
	args['levelup']-=1
	jump 'levelup'
end
if $args['lup']<>$args[0]: $args['next']+="gosub 'dialog.replic.int','<<$args['lup']>>'
"
! поиск реплики для перехода по меткам
:leveljump
args['instr']=instr($args['source'],'leveljump:')					&	!	вхождение слова команды
if args['instr']<>0 and args['i']<99999:
! если команда присутствует в исходнике
	$args['jump']=$func('get.tag.num',$args['source'],'leveljump')
	! нашли на какую метку нужно перейти
	if $args['jump']<>'':
	! если метка есть ищем реплику с такой меткой
		args['replic.marked']=-1
		:marked_replics
		args['replic.marked']=func('#chk.array.word#','$source_replic','marker:'+$args['jump'],args['replic.marked']+1)
		if args['replic.marked']>0 and $position_replic[args['replic.marked']]<>'':
		! если реплика найдена, добавляем её к интерпретируемым репликам - список
			$args['next']+="gosub 'dialog.replic.int','<<$id_replic[args['replic.marked']]>>'
"
		end
		$args['source']=$replace($args['source'],'leveljump:'+$args['jump'])
		if instr($args['source'],'leveljump:')<>0 and args['i']<99999:
			args['i']+=1
			jump 'marked_replics'
		end
	else
	! если метка пустая
		$args['comm']=$mid($args['source'],args['instr'],11)		&	!	вырезаем команду и следующий за ней символ
		$args['m']=$mid($args['comm'],11)							&	!	вырезаем последний символ
$args['\n\r']='
'
		! если последний символ пробельный или перевод строки, убираем команду из реплики
		if strcomp($args['m'],'\s|'+$args['\n\r'])=-1: $args['source']=$replace($args['source'],$args['comm'])
	end
	args['i']+=1
	jump 'leveljump'
end
if instr($args['next'],'gosub')<>0:
	$args['next']="$DIALOG_VALUE['list.act.del']=''
"+$args['next']
end
$result=$args['next']
--- dialog.get.jump ---------------------------------