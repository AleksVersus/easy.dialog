QSP-Game Интерпретатор отдельной реплики
$args[0] -	идентификатор реплики
# dialog.replic.int
!@ pl 'replic.int: <<$args[0]>>' & wait 250
!@ ------------------- многократно используемы локальные переменные ------------------- 
	local $code_
!@ ------------------- многократно используемы локальные переменные ------------------- 
local $rcode_ = $dialogs_run[$args[0]]	&	!@	Динамический код, повешенный на реплику
local $now_sets_ = @dialog.get.sets($args[0])	& !@ текущие настройки, восстановленные до корня ветки

if $rcode_ <> '': dynamic $rcode_	&	!@	выполняем код текущей реплики
@dialog.replic.print($args[0], $now_sets_) & !@	обновляем лог реплик

!@ снимаем фиксацию с реплики
$DIALOG_VALUE['list.act.fix'] = @edb.list.remove($DIALOG_VALUE['list.act.fix'], $args[0])

!@ Рекурсия к вложенным репликам. Получаем список реплик
local i=0	&	!@	общий счётчик проходов
local a=0	&	!@	счётчик ответных реплик
local q=0	&	!@	счётчик вопросных реплик

!@ добавляем фиксированные ответные реплики в список
local $answer_pos_ & !@ массив для списка ответных реплик
$code_ = { $answer_pos_[a] = $args[0] & a += 1 }
@edb.list.for_each($DIALOG_VALUE['list.act.fix'], $code_)

!@ ---------------------------- перебираем дочерние реплики и составляем два списка ---------------------------- 
	local $rdaughters_ = $dialogs_includes[$args[0]]&	!@	список дочерних
	local maximal_, minimal_, REPLICS_COUNT_, $REPLICS_ID_
	$code_ = {
		if @dialog.replic.prv.if($args[0]):
			!@ если реплика прошла проверку на соответствие условиям
			!@ определяем тип реплики
			local $type_ = @em.tag.getNum($dialogs_sets[$args[0]], 'type')
			if $type_ = 'quest':
				!@ если это реплика неписи (пассивная)
				local pos_ = @dialog.mt.add($args[0])
				if maximal_['count'] < REPLICS_COUNT_[pos_]:
					!@ если максимальное из просмотреннных значений меньше текущего, выбираем его
					maximal_['count'] = REPLICS_COUNT_[pos_] &	!@	запоминаем новое максимальное значение
					maximal_['pos'] = pos_					 &	!@	запоминаем положение значения в микробазе
				end
				if minimal_['count'] >= REPLICS_COUNT_[pos_]:
				! если минимальное из просмотреннных значений больше текущего или равно, выбираем его
					minimal_['count'] = REPLICS_COUNT_[pos_] &	!@	запоминаем новое минимальное значение
					minimal_['pos'] = pos_					 &	!@	запоминаем положение значения в микробазе
				end
				!@ если минимальное количество равно нулю, а минимальное значение в микробазе — нет,
				!@ минимальное количество выставляем по минимальному в микробазе
				if minimal_['count'] = 0 and min('REPLICS_COUNT_')<>0: minimal_['count'] = min('REPLICS_COUNT_')
				q+=1
			end
			!@ если это реплика героя (ответная), запоминаем её позицию в базе
			if $type_ = 'answer': $answer_pos_[a] = $args[0] & a += 1
		end
	}
	@edb.list.for_each($rdaughters_, $code_)
!@ ---------------------------- перебираем дочерние реплики и составляем два списка ---------------------------- 


local $rsets_ = $dialogs_sets[$args[0]]		&	!@	настройки реплики
local $rtype_ = @em.tag.getNum($rsets_, 'type') &	!@	Тип реплики
local $replics_repeat_ = @em.tag.getNum($now_sets_, 'repeat')	& !@ число повторов реплики
local $replics_shuffle_ = @em.tag.getNum($now_sets_, 'shuffle')	& !@ порядок воспроизведения

!@ когда есть список активируемых реплик. Выгружаются все.
if a > 0:
	!@ Если текущая реплика пассивная, очищаем список кнопок на экране; в противном случае — дополняем:
	if $rtype_ = 'quest': killvar '$DIALOG_BUTTONS'
	loop local i = 0 while i<a step i += 1:
		if $dialogs_id[$answer_pos_[i]]<>'' and no @edb.list.is_el($DIALOG_VALUE['list.act.del'], $answer_pos_[i]):
			$DIALOG_BUTTONS[] = @dialog.int.answer($answer_pos_[i], $now_sets_)
		end
	end
end

!@ обновляем список кнопок
@dialog.btns.refresh()

!@ перерисовываем кадр
@dialog.screen.reprint()

!@ список реплик неписи. Из набора реплик выбирается одна
if q > 0:
	if $replics_repeat_='one':
		!@ Если фраза выводится всё время одна и та же.
		if maximal_['count'] <> minimal_['count']:
			!@ Если минимальное и максимальное значения не совпадают, значит одна из фраз уже выбрана.
			!@ Уничтожаем все реплики, кроме выбранной:
			@dialog.mt.kill_other($REPLICS_ID_[maximal_['pos']])
			jump 'choose_frase'
		end
	elseif $replics_repeat_='once':
		!@ Если реплики выводятся один круг и после выводится только последняя
		if maximal_['count'] = minimal_['count'] and minimal_['count'] <> 0:
			!@ если минимум и максимум совпадают, снимаем метки со всех реплик списка
			@dialog.mt.del_count()
			jump 'choose_frase'
		elseif maximal_['count'] <> minimal_['count'] and minimal_['count'] = 0:
			!@ Если метки стоят на паре фраз, а на других не стоят, выбираем только неотмеченные реплики
			@dialog.mt.kill_onced()
			!@ если остаётся только одна невыбранная реплика, сразу задираем ей количество
			if arrsize('$REPLICS_ID_') = 1: dialogs_count[$REPLICS_ID_[0]] += 1
			jump 'choose_frase'
		elseif maximal_['count'] <> minimal_['count'] and minimal_ > 0['count']:
			!@ Если реплики уже выбирались и на одной из них есть метка последней, выбираем эту реплику
			@dialog.mt.kill_other($REPLICS_ID_[maximal_['pos']])
		end
	elseif $replics_repeat_='cicle':
		if maximal_['count'] = minimal_['count']:
			@dialog.mt.del_count()
		else
			@dialog.mt.kill_onced()
		end
	end
	:choose_frase
	local $choosing_frase_ = ''
	if $replics_shuffle_ = 'random':
		$choosing_frase_ = $REPLICS_ID_[rand(0,arrsize('$REPLICS_ID_')-1)]
	elseif $replics_shuffle_='straight':
		$choosing_frase_ = $REPLICS_ID_[0]
	end
	if $choosing_frase_<>'': @dialog.int.quest($choosing_frase_)
end
!@ закрытие диалога происходит после выполнения кода и вывода реплики на экран:
if instr($rsets_, '[closeup]')<>0: @dialog.closeup() & exit	
--- dialog.replic.int ---------------------------------


