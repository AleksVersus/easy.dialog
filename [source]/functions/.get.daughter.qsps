# dialog.get.daughter
! Функция получает все дочерние объекты для данного ай-ди.
! Результатом является true - есть дочерние, false - нет дочерних
$args[0] = $args[0] & ! $args[0] - ай-ди родительского объекта
$args[1] = $args[1] & ! $args[1] - регулярное выражение по которому производится дополнительная фильтрация
if $args[2] = '': $args[2] = '$rbTEMP_ID'	&	!	массив, в который вносятся ай-ди дочерних объектов.
if instr($args[2],'$')<>1: $args[2] = '$'+$args[2]
$args[3] = $args[3]	&	!	дополнительная проверка.
$args[4] = $args[4]	&	!	generate - режим генерации
if $args[4]='generate':
	$args['position.array']='$easy_dialog_base_PS'
	$args['id.array']='$easy_dialog_base_ID'
else
	$args['position.array']='$position_replic'
	$args['id.array']='$id_replic'
end
! регулярное выражение по которому производится дополнительная фильтрация. Сортировка идёт по данным в массиве $run_array[]
$args['gdo']='false'
args['pos']=-1
:for
args['pos']=arrpos(args['pos']+1,$args['position.array'],$args[0])
if args['pos']<>-1 and args['i']<>args['pos']:
	args['i']=args['pos']
	if $args[1]='' or $strfind($source_replic[args['pos']],$args[1])<>'' or ($args[4]='generate' and $strfind($easy_dialog_base_SC[args['pos']],$args[1])<>''):
		!if $args[3]='' or $strfind($run_array[args['pos']],$args[3])!'':
			$args['gdo']='true'
			dynamic "<<$args[2]>>[arrsize('<<$args[2]>>')]=<<$args['id.array']>>[args[0]]",args['pos']
		!end
	end
	jump 'for'
end
$result=$args['gdo']
--- dialog.get.daughter ---------------------------------