# dialog.screen.save
! данная функция определяет содержимое окон до диалога и во время
!if $args[0]<>'': *clr & *pl 'Тестовый режим: ' & *pl $args[0]
$DIALOG_VALUE['screen.main']=$maintxt	&	!	сохраняем содержимое окна основного описания
$DIALOG_VALUE['screen.acts']=$curacts	&	!	сохраняем содержимое окна действий
$DIALOG_VALUE['screen.stat']=$stattxt	&	!	сохраняем содержимое окна дополнительного описания
DIALOG_VALUE['screen.html']=usehtml		&	!	запоминаем включен или выключен html рендерер

if $DIALOG_VALUE['answer.view']<>'answer.href':
! если ответные реплики воспроизводятся в виде действий
	cls	&	!	очищаем всё, кроме списка предметов
else
	*clr & clr 	&	!	очищаем только окна с описаниями
	if instr($DIALOG_VALUE['screen.main'],'<avs-dialog_div>')<>0:
		args['summ']+=4
		$args['t.d']=$strfind($DIALOG_VALUE['screen.main'],"<avs-dialog_div>[\s\S]*?<\/avs-dialog_div>")
	else
		$args['t.d']="<avs-dialog_div>&nbsp;</avs-dialog_div>"
	end
	if instr($DIALOG_VALUE['screen.main'],'<avs-head_div>')<>0:
		args['summ']+=2
		$args['t.h']=$strfind($DIALOG_VALUE['screen.main'],"<avs-head_div>[\s\S]*?<\/avs-head_div>")
	else
		$args['t.h']="<avs-head_div>&nbsp;</avs-head_div>"
	end
	if instr($DIALOG_VALUE['screen.main'],'<avs-answer_div>')<>0:
		args['summ']+=1
		$args['t.a']=$strfind($DIALOG_VALUE['screen.main'],"<avs-answer_div>[\s\S]*?<\/avs-answer_div>")
	else
		$args['t.a']="<avs-answer_div>&nbsp;</avs-answer_div>"
	end
	! D 0 0 0 0 1 1 1 1 
	! H 0 0 1 1 0 0 1 1
	! A 0 1 0 1 0 1 0 1
	!   0 1 2   4     7
	pl args['summ']	&	!	УДАЛИТЬ ИЗ ОКОНЧАТЕЛЬНОЙ ВЕРСИИ МОДУЛЯ
	if args['summ']=0:
	! ни один из тегов не присутствует в исходном окне, создаём новое окно
		$DIALOG_VALUE['screen.now']=$args['t.h']+"<br>"
		$DIALOG_VALUE['screen.now']+=$args['t.d']+"<br>"
		$DIALOG_VALUE['screen.now']+=$args['t.a']
	else
	! если хоть один из тегов присутствует в исходном окне:
		$DIALOG_VALUE['screen.now']=$DIALOG_VALUE['screen.main']
		! теперь без записи длинных условий:
		if args['summ']>3:
		! если сумма больше трёх, однозначно есть тег диалога:
			if args['summ']-4=0 or args['summ']-4=2:
			! отсутствует тег ответов, либо оба оставшиеся
				$DIALOG_VALUE['screen.now']=$replace($DIALOG_VALUE['screen.now'],$args['t.d'],$args['t.d']+'<br>'+$args['t.a'])
			end
			if args['summ']-4=0 or args['summ']-4=1:
			! отсутствует тег заголовка, либо оба оставшиеся
				$DIALOG_VALUE['screen.now']=$replace($DIALOG_VALUE['screen.now'],$args['t.d'],$args['t.h']+'<br>'+$args['t.d'])
			end
		else
		! если сумма не больше трёх, значит тег диалога отсутствует
			if args['summ']-1=0:
			! если здесь получился ноль, значит есть блок ответов, но нет блока заголовка
				$DIALOG_VALUE['screen.now']=$replace($DIALOG_VALUE['screen.now'],$args['t.a'],$args['t.h']+'<br>'+$args['t.a'])
			elseif args['summ']-2=0:
			! если здесь получился ноль, значит есть блок заголовка, но нет блока ответов
				$DIALOG_VALUE['screen.now']=$replace($DIALOG_VALUE['screen.now'],$args['t.h'],$args['t.h']+'<br>'+$args['t.a'])
			end
			! теперь точно есть и блок ответов и блок заголовка, добавляем блок диалога
				$DIALOG_VALUE['screen.now']=$replace($DIALOG_VALUE['screen.now'],$args['t.a'],$args['t.d']+'<br>'+$args['t.a'])
		end
	end
end
*p $DIALOG_VALUE['screen.now']
!if $args[0]<>'': usehtml=0 & *pl $DIALOG_VALUE['screen.now']
--- dialog.screen.save ---------------------------------