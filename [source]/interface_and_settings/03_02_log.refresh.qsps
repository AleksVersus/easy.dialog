QSP-Game Обновление на экране списка реплик из лога реплик

# dialog.log.refresh
local log_strings_ = val(@em.tag.getNum($dialogs_sets[$DIALOG_VALUE['dialog_id']], 'strings'))
local dsrn_ = iif(log_strings_, log_strings_, DIALOG_VALUE['default_screen_replics_number'])
local separator_ = arrpos('$DIALOG_REPLIC_LOG', '<avs-hide-replics>')
local counter_pos_ = arrpos('$counter', 'dialog.timer')
if separator_ <> -1:
	$DIALOG_REPLIC_LOG[separator_], $DIALOG_REPLIC_LOG[separator_+1] = $DIALOG_REPLIC_LOG[separator_+1], $DIALOG_REPLIC_LOG[separator_]
	separator_ += 1
	if separator_ = arrsize('$DIALOG_REPLIC_LOG') - 1:
		killvar '$DIALOG_REPLIC_LOG', separator_
		killvar '$counter', counter_pos_
		DIALOG_VALUE['timer_start'] = 1
	elseif counter_pos_ = -1:
		$counter[] = 'dialog.timer'
		settimer 100
	end
else:
	separator_ = arrsize('$DIALOG_REPLIC_LOG')
end
local $log_ = ''
loop local i = iif(separator_ - dsrn_ <= 0, 0, separator_ - dsrn_) while i < separator_ step i += 1:
	$log_ += $DIALOG_REPLIC_LOG[i]
end
$log_ = '<div class="avs_dialog_replics"><<$log_>></div class="avs_dialog_replics">'
local $replics_div_ = $strfind($DIALOG_VALUE['screen_now'], '<div class="avs_dialog_replics">[\s\S]*?<\/div class="avs_dialog_replics">')
$DIALOG_VALUE['screen_now'] = $replace($DIALOG_VALUE['screen_now'], $replics_div_, $log_)
--- dialog.log.refresh ---------------------------------
